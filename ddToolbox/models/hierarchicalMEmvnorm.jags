model{

# comparison acuity (alpha)
groupALPHAmu        ~ dunif(0,100)
groupALPHAsigma     ~ dunif(0,100)

# error rates (epsilon)
groupW          ~  dbeta(1.1, 10.9)  # mode for lapse rate
groupKminus2    ~  dgamma(0.01,0.01) # concentration parameter
groupK          <- groupKminus2+2




for (p in 1:(nRealExperimentFiles)){      # FIX FOR UNOBSERVED PARTICIPANT

    precision[p,1] ~ dgamma(0.1,0.1)
    precision[p,2] ~ dgamma(0.1,0.1)
    mc_sigma[p,1] <- 1/sqrt(precision[p,1]) # sigma over m
    mc_sigma[p,2] <- 1/sqrt(precision[p,2]) # sigma over c


    mc_mu[p,1] ~ dnorm(-0.243, 1/( (0.027)^2))    # prior over m_mu
    mc_mu[p,2] ~ dnorm(-3    , 1/( 2^2) )           # prior over c_mu
    r[p]       ~ dnorm(-0.9  , 1/0.1^2) T(-1,1)  #dunif(-1,1)

    # Reparamaterize
    T[p,1,1] <- 1/precision[p,1]                        # ie variance
    T[p,1,2] <- r[p] * mc_sigma[p,1] * mc_sigma[p,2]
    T[p,2,1] <- r[p] * mc_sigma[p,1] * mc_sigma[p,2]
    T[p,2,2] <- 1/precision[p,2]
    TI[p,1:2,1:2] <- inverse(T[p,1:2,1:2])              # ie variance

	mc[p,1:2]   ~ dmnorm(mc_mu[p,1:2],TI[p,1:2,1:2])

	# just for ease of plotting variables m, c...
	m[p] <- mc[p,1]
	c[p] <- mc[p,2]

	epsilon[p]  ~ dbeta(groupW*(groupK-2)+1 , (1-groupW)*(groupK-2)+1 ) T(,0.5)
	alpha[p]    ~ dnorm(groupALPHAmu, 1/(groupALPHAsigma^2)) T(0,)
}

for (t in 1:length(ID)) {
	# Calculate log discount rate for each reward
	lkA[t] <- mc[ID[t],1] * log(abs(A[t])) + mc[ID[t],2]
	lkB[t] <- mc[ID[t],1] * log(abs(B[t])) + mc[ID[t],2]

	# calculate present subjective value for each reward
	VA[t] <- A[t] / (1+ ( exp(lkA[t]) * DA[t]))
	VB[t] <- B[t] / (1+ ( exp(lkB[t]) * DB[t]))

	# Psychometric function
	P[t] <- epsilon[ID[t]] + (1-2*epsilon[ID[t]]) * phi( (VB[t]/VA[t]) / alpha[ID[t]] )

	# response likelihood
	R[t]         ~ dbern(P[t]) # likelihood of actual response
	Rpostpred[t] ~ dbern(P[t]) # posterior predicted response
}

}
